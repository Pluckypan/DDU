apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: rootProject.ext.protoBuf.plugin

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    defaultConfig {
        applicationId rootProject.ext.app.id
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.app.verCode
        versionName rootProject.ext.app.verName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    dataBinding {
        enabled = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            proto {
                srcDir 'src/main/proto'
                include '**/*.proto'
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // build
    implementation rootProject.ext.build.kotlinLib
    // androidx
    implementation rootProject.ext.androidx.appcompat
    implementation rootProject.ext.androidx.ktx
    implementation rootProject.ext.androidx.constraintlayout
    implementation rootProject.ext.androidx.recyclerview
    // framework
    implementation rootProject.ext.framework.oneActivity
    implementation rootProject.ext.framework.autoFlowLayout
    implementation rootProject.ext.framework.easyPermissions
    implementation rootProject.ext.framework.viewAnimator
    implementation rootProject.ext.framework.liveBus
    implementation rootProject.ext.framework.mmkv
    implementation rootProject.ext.framework.workManager.runtime
    implementation rootProject.ext.framework.workManager.firebase
    kapt rootProject.ext.framework.room.compiler
    implementation rootProject.ext.framework.room.runtime
    implementation rootProject.ext.framework.room.rxjava2
    // test
    testImplementation rootProject.ext.test.junit
    androidTestImplementation rootProject.ext.test.runner
    androidTestImplementation rootProject.ext.test.espresso
    // ProtocolBuffer
    implementation rootProject.ext.protoBuf.java
    implementation rootProject.ext.protoBuf.javaFormat
    // Widgets
    implementation rootProject.ext.widget.syntaxView
    // module
    implementation project(':whisper')
    implementation project(':imessenger')
    implementation project(':easyprinter')
}


protobuf {
    protoc {
        artifact = rootProject.ext.protoBuf.compile
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.builtins {
                java {}
                cpp {}
            }
        }
    }
    //生成目录
    generatedFilesBaseDir = "$projectDir/src/generated"
}